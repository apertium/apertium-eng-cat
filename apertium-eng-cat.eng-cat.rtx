! POS

n: _.gender.number ;
np: _.np_type.gender.number ;
det: _.det_type.gender.number ;
adj: _.gender.number ;
num: _.gender.number ;
num_digits: _ ;
adv: _ ;
vblex: _.tense.person.number ;
vbser: _.tense.person.number ;
vb_infger: _.tense ;

SN: _.SN_det.gender.number ;
SDet: _.det_type.gender.number ;
SAdj: _.gender.number ;
SNum: _.gender.number ;
SV: _.vb_type.tense.gender.number ;

PV: _;


! Attributes

gender = (GD m) m f nt @mf GD ;
number = (ND sg) sg pl @sp ND ;
person = (PD p3) p1 p2 p3 PD ;
tense = pres pri past ifip fti cni inf ger ;
separable = sep ;
det_type = def ind pos qnt ;
np_type = al org top ant cog ;
vb_type = vblex vbser vblike ;
SN_det = undet det predet ;


! Rules

SN ->   0: n
                [$SN_det=undet,$gender=(if(1.gender not=mf) 1.gender else GD),$number=(if(1.number not=sp) 1.number else ND)]
                { 1[gender=$gender,number=$number] } |
        0: np.top
                [$SN_det=undet,$gender=(if(1.gender not=mf) 1.gender else GD),$number=(if(1.number not=sp) 1.number else ND)]
                { 1[gender=$gender,number=$number] } |
        1: SDet SN.undet.$gender.$number
                [$SN_det=det]
                { 1[gender=$gender,number=$number] _1 2[gender=$gender,number=$number] } |
        2: SNum SN.undet.$gender.$number
                [$SN_det=undet]
                { 1[gender=$gender,number=$number] _1 2[gender=$gender,number=$number] } |
        2: SAdj SN.undet.$gender.$number
                [$SN_det=undet]
                { 2[gender=$gender,number=$number] _1 1[gender=$gender,number=$number] } ;

SDet -> 0: det.$det_type.$gender.$number
                { 1[gender=$gender,number=$number] } ;

SAdj -> 0: adj.$gender.$number
                { 1[gender=$gender,number=$number] } |
        1: adj.$gender.$number enough@adv
                { 2 _1 1[gender=$gender,number=$number] } ;

SNum -> 0: num.$gender.$number
                { (if($number not = sp) 1(num_digits) else 1[gender=$gender,number=$number]) } ;


SV ->   0: vblex.*.pres.$person.$number
                [$vb_type=vblex,$tense=pri]
                { 1[tense=$tense,person=$person,number=$number] } |
        0: vblex.*.past.$person.$number
                [$vb_type=vblex,$tense=ifip]
                { anar@vaux.$person.$number _ 1(vb_infger)[tense=inf] } |
        0: will@vbmod.pres vblex.*.inf
                [$vb_type=vblex,$tense=fti]
                { 2[tense=$tense,person=$person,number=$number] } |
        0: will@vbmod.past vblex.*.inf
                [$vb_type=vblex,$tense=cni]
                { 2[tense=$tense,person=$person,number=$number] } ;



PV ->   0: SN SV.vblex
                { 1 _1 2[person=p3,number=1.number] } ;
